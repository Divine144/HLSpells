buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://maven.parchmentmc.org' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

repositories {
    // JEI
    maven {
        // Location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        // Location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }
    maven {
        name = "Curios maven"
        url = "https://maven.theillusivec4.top/"
    }
    maven { url = 'https://repo.spongepowered.org/maven' }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply from: 'https://raw.githubusercontent.com/SizableShrimp/Forge-Class-Remapper/main/classremapper.gradle'

version = project.mod_version
group = 'com.divinity'
archivesBaseName = project.mod_id

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
compileJava {
    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
}

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'official', version: '1.18.2'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            workingDirectory project.file('run')
            arg "-mixin.config=hlspells.mixin.json"
            property 'forge.logging.console.level', 'debug'
        }

        server {
            workingDirectory project.file('run')
            arg "-mixin.config=hlspells.mixin.json"
            property 'forge.logging.console.level', 'debug'
        }
    }
}

mixin {
    add sourceSets.main, "hlspells.refmap.json"
}

dependencies {
    minecraft "net.minecraftforge:forge:1.18.2-40.0.44"
    // Compile against the API but do not include it at runtime
    compileOnly fg.deobf("mezz.jei:jei-${jei_mc_version}:${jei_version}:api")
    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}:api")
    // At runtime, use the full mod jar
    runtimeOnly fg.deobf("mezz.jei:jei-${jei_mc_version}:${jei_version}")
    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}")
    // Processes mixin annotations
    annotationProcessor 'org.spongepowered:mixin:0.8.5-SNAPSHOT:processor'
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "hlspells",
                "Specification-Vendor"    : "hlspellssareus",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.mod_version,
                "Implementation-Vendor"   : "hlspellssareus",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "hlspells.mixin.json"
        ])
    }
    rename 'mixin.refmap.json', 'hlspells.refmap.json'
}